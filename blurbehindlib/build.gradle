apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    jcenter()
}
project.version = "1.0.4"

android {
    namespace = "no.danielzeller.blurbehindlib"
    compileSdk 34

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

tasks.withType(Javadoc) {
       enabled = false
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'blurbehindlib'

    publishedGroupId = 'no.danielzeller.blurbehindlib'
    libraryName = 'BlurBehindLib'
    artifact = 'blurbehindlib'

    libraryDescription = 'This is a Library fir blurring the background of a View.'

    siteUrl = 'https://github.com/danielzeller/Blur-LIB-Android'
    gitUrl = 'https://github.com/danielzeller/Blur-LIB-Android.git'

    developerId = 'danielzeller'
    developerName = 'Daniel Zeller'
    developerEmail = 'daniel@danielzeller.no'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name "GitHubPackages"

                url = uri("https://maven.pkg.github.com/NYOUM/Blur-LIB-Android")

                credentials {
                    username = getProperty("gpr.usr") ?: System.getenv("GPR_USER")
                    password = getProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release
                groupId = publishedGroupId
                artifactId = artifact
                version project.version

                pom {
                    name = libraryName
                    description = libraryDescription
                    url = siteUrl
                    inceptionYear = '2016'
                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = developerName
                        }
                    }
                    scm {
                        connection = "scm:git:$gitUrl"
                        url = siteUrl
                    }
                }
            }
        }
    }
}
